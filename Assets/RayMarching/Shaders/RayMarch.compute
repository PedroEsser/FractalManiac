#pragma kernel CSMain
#include "Assets/RayMarching/Shaders/SDFs.hlsl"
#include "Assets/RayMarching/Shaders/Fractal_SDFs.hlsl"

#define MAX_STEPS 200
#define MAX_DIST 1000.0
#define EPSILON 0.001

RWTexture2D<float4> Result;

float4x4 _CameraToWorld;
float4x4 _CameraInverseProjection;
float3 _CameraWorldPos;
float4 _ScreenParams;
float3 _PlayerPos;
float _PlayerSize;

float playerSDF(float3 pos)
{
    return sphere_sdf(pos - _PlayerPos, _PlayerSize);
}

float playerClipSDF(float3 pos)
{
    return sphere_sdf(pos - _PlayerPos, 10 * _PlayerSize);
}

float sceneSDF(float3 pos)
{
    //return torus_sdf(pos, 60, 10);
    float fractal_dist = abs(mandelbulbSDF(pos, 30));
    float dist_scene = max(fractal_dist, -playerClipSDF(pos));
    
    return min(dist_scene, playerSDF(pos));
    //return fractal_dist;
}

float3 RayMarchScene(float3 rayOrigin, float3 rayDir)
{
    float totalDist = 0.0;

    for (int i = 0; i < MAX_STEPS; i++)
    {
        float3 pos = rayOrigin + rayDir * totalDist;
        float dist = sceneSDF(pos);

        if (dist < EPSILON){
            float3 col = playerSDF(pos) < EPSILON ? float3(1, 1, 0) : float3(1, 1, 1);
            return col * exp(-totalDist);
        }

        totalDist += dist;
        if (totalDist > MAX_DIST)
            break;
    }

    return float3(0, 0, 0);
}

[numthreads(8,8,1)]
void CSMain(uint3 id : SV_DispatchThreadID)
{
    float2 resolution = _ScreenParams.xy;
    float2 uv = (id.xy / resolution) * 2.0 - 1.0;
    uv.x *= resolution.x / resolution.y;

    // reconstruct view ray in world space
    float4 rayCam = mul(_CameraInverseProjection, float4(uv, 1, 1));
    rayCam /= rayCam.w;

    float3 rayDir = normalize(mul((float3x3)_CameraToWorld, rayCam.xyz));
    float3 rayOrigin = _CameraWorldPos;

    float3 color = RayMarchScene(rayOrigin, rayDir);
    Result[id.xy] = float4(color, 1.0);
}
